plugins {
    id 'java'
    id 'application'
}

group 'nayan92'
version '1.0-SNAPSHOT'

mainClassName = 'nayan92.moneytransfer.App'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    functionalTest {
        java.srcDir "$projectDir/src/functional/java"
        resources.srcDir "$projectDir/src/functional/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestCompile.extendsFrom integrationTestCompile
    functionalTestRuntime.extendsFrom functionalTestCompile
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn integrationTest
check.dependsOn functionalTest
integrationTest.mustRunAfter test
functionalTest.mustRunAfter integrationTest

dependencies {
    compile "com.sparkjava:spark-core:2.8.0"
    compile "org.slf4j:slf4j-api:1.7.28"
    compile "org.slf4j:slf4j-simple:1.7.28"
    compile "com.google.inject:guice:4.2.2"
    compile "com.h2database:h2:1.4.199"
    compile "org.jdbi:jdbi3-core:3.10.1"
    compile "org.flywaydb:flyway-core:6.0.4"
    compile "com.google.code.gson:gson:2.8.5"

    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest:2.1"
    testCompile "org.mockito:mockito-core:3.0.0"

    functionalTestCompile 'io.rest-assured:rest-assured:4.1.1'
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
}
